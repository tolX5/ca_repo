
Процедура Установить(Набор) Экспорт

	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	УО_НаборыДанных.Владелец КАК Схема,
	                      |	УО_НаборыДанных.Родитель КАК Группа,
	                      |	УО_НаборыДанных.Ссылка КАК Набор,
	                      |	УО_Индексы.Индекс КАК Индекс
	                      |ПОМЕСТИТЬ вт_ТекущийНабор
	                      |ИЗ
	                      |	Справочник.УО_НаборыДанных КАК УО_НаборыДанных
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УО_Индексы КАК УО_Индексы
	                      |		ПО УО_НаборыДанных.Владелец = УО_Индексы.Схема
	                      |			И УО_НаборыДанных.Родитель = УО_Индексы.Группа
	                      |			И УО_НаборыДанных.Ссылка = УО_Индексы.Набор
	                      |ГДЕ
	                      |	УО_НаборыДанных.Ссылка = &Набор
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	УО_Индексы.Схема КАК Схема,
	                      |	УО_Индексы.Группа КАК Группа,
	                      |	МАКСИМУМ(УО_Индексы.Индекс) КАК Индекс
	                      |ПОМЕСТИТЬ вт_МаксимальныйИндекс
	                      |ИЗ
	                      |	вт_ТекущийНабор КАК вт_ТекущийНабор
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УО_Индексы КАК УО_Индексы
	                      |		ПО вт_ТекущийНабор.Схема = УО_Индексы.Схема
	                      |			И вт_ТекущийНабор.Группа = УО_Индексы.Группа
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	УО_Индексы.Схема,
	                      |	УО_Индексы.Группа
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	вт_ТекущийНабор.Схема КАК Схема,
	                      |	вт_ТекущийНабор.Группа КАК Группа,
	                      |	вт_ТекущийНабор.Набор КАК Набор,
	                      |	ЕСТЬNULL(вт_МаксимальныйИндекс.Индекс + 1, 0) КАК Индекс
	                      |ИЗ
	                      |	вт_ТекущийНабор КАК вт_ТекущийНабор,
	                      |	вт_МаксимальныйИндекс КАК вт_МаксимальныйИндекс
	                      |ГДЕ
	                      |	вт_ТекущийНабор.Индекс ЕСТЬ NULL");	
	
	Запрос.УстановитьПараметр("Набор", Набор);
	
	Результат = Запрос.Выполнить();
	
	Выборка = Результат.Выбрать();	
	
	Пока Выборка.Следующий() Цикл
		Движение(Выборка).Записать();
	КонецЦикла;
	
КонецПроцедуры

Процедура Удалить(Набор) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	УО_Индексы.Схема КАК Схема,
		|	УО_Индексы.Группа КАК Группа,
		|	УО_Индексы.Индекс КАК Индекс
		|ИЗ
		|	РегистрСведений.УО_Индексы КАК УО_Индексы
		|ГДЕ
		|	УО_Индексы.Набор = &Набор";
	
	Запрос.УстановитьПараметр("Набор", Набор);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Движение(Выборка).Удалить();
	// @skip-check query-in-loop
		ОбновитьИндексы(Выборка.Схема, Выборка.Группа);
	КонецЦикла;

КонецПроцедуры

Функция Движение(Данные)

	Движение = СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(Движение, Данные);
	Возврат Движение;

КонецФункции

Процедура ОбновитьИндексы(Схема, Группа)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	УО_Индексы.Схема КАК Схема,
		|	УО_Индексы.Группа КАК Группа,
		|	УО_Индексы.Индекс КАК Индекс,
		|	УО_Индексы.Набор КАК Набор,
		|	АВТОНОМЕРЗАПИСИ() КАК НомерЗаписи
		|ПОМЕСТИТЬ вт_Индексы
		|ИЗ
		|	РегистрСведений.УО_Индексы КАК УО_Индексы
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Индекс
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	вт_Индексы.Схема КАК Схема,
		|	вт_Индексы.Группа КАК Группа,
		|	вт_Индексы.Набор КАК Набор,
		|	вт_Индексы.НомерЗаписи - 1 КАК Индекс
		|ИЗ
		|	вт_Индексы КАК вт_Индексы";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	НаборЗапискй = СоздатьНаборЗаписей();
	НаборЗапискй.Отбор.Схема.Установить(Схема);
	НаборЗапискй.Отбор.Группа.Установить(Группа);
	НаборЗапискй.Загрузить(РезультатЗапроса.Выгрузить());
	НаборЗапискй.Записать();
	
КонецПроцедуры

Процедура ПроверитьСоседниеНаборы(Набор, ЕстьВыше, ЕстьНиже) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	УО_Индексы.Схема,
		|	УО_Индексы.Группа,
		|	УО_Индексы.Индекс
		|ПОМЕСТИТЬ втТекущий
		|ИЗ
		|	РегистрСведений.УО_Индексы КАК УО_Индексы
		|ГДЕ
		|	УО_Индексы.Набор = &Набор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НЕ МАКСИМУМ(Предыдущий.Индекс) ЕСТЬ NULL КАК ЕстьВыше,
		|	НЕ МИНИМУМ(Следующий.Индекс) ЕСТЬ NULL КАК ЕстьНиже
		|ИЗ
		|	втТекущий КАК втТекущий
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УО_Индексы КАК Предыдущий
		|		ПО втТекущий.Схема = Предыдущий.Схема
		|		И втТекущий.Группа = Предыдущий.Группа
		|		И втТекущий.Индекс > Предыдущий.Индекс
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УО_Индексы КАК Следующий
		|		ПО втТекущий.Схема = Следующий.Схема
		|		И втТекущий.Группа = Следующий.Группа
		|		И втТекущий.Индекс < Следующий.Индекс";
	
	Запрос.УстановитьПараметр("Набор", Набор);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ЕстьНиже = ВыборкаДетальныеЗаписи.ЕстьНиже;
		ЕстьВыше = ВыборкаДетальныеЗаписи.ЕстьВыше;
	КонецЦикла;
	
КонецПроцедуры
