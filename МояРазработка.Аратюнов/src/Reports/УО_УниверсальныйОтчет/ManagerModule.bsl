
Процедура ПолучитьСписокМакетов(Список) Экспорт

	Для каждого ОбъектМетаданных Из Метаданные.Отчеты.УО_УниверсальныйОтчет.Макеты Цикл
		Список.Добавить(ОбъектМетаданных.Имя, ОбъектМетаданных.Представление());
	КонецЦикла;

КонецПроцедуры

// Вытаскиваем схему под соответствующего пользователя
Функция ТекущаяСхема() Экспорт

	ИмяСхемы = ИмяСхемы();
	
	Если ИмяСхемы <> Неопределено Тогда
		ИдентификаторСхемы = Новый УникальныйИдентификатор(ИмяСхемы);
		РегистрацияСхемы = Справочники.УО_РегистрацияСхемы.ПолучитьСсылку(ИдентификаторСхемы);
		Возврат РегистрыСведений.УО_ХранилищеСхем.Схема(РегистрацияСхемы);
	КонецЕсли;
	
КонецФункции

// Событие до создания формы
Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)

	Если ВидФормы = "Форма" Тогда
		// Сохранение имени
		Если Параметры.Свойство("КлючНазначенияИспользования") Тогда
			ХранилищеОбщихНастроек.Сохранить("Отчет.УО_УниверсальныйОтчет.ТекущаяСхема",,
				Параметры.КлючНазначенияИспользования,,ИмяПользователя());
		Иначе
			Параметры.Вставить("КлючНазначенияИспользования", ИмяСхемы());
			СтандартнаяОбработка = Ложь;
			ВыбраннаяФорма = "ФормаОтчета";
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Функция ИмяСхемы()

	Возврат ХранилищеОбщихНастроек.Загрузить("Отчет.УО_УниверсальныйОтчет.ТекущаяСхема",,,
		ИмяПользователя());		

КонецФункции


