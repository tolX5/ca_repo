

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	Если ВидФормы = "ФормаОбъекта" Тогда
		// У нового элемента такого параметра не будет, а у существующего будет
		Если Параметры.Свойство("Ключ") Тогда
			СтандартнаяОбработка = Ложь;
			ВыбраннаяФорма = "ФормаЭлемента_" + Параметры.Ключ.ВидНабора; 
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Функция ЯвляетсяПодчиненным(ТекущийНабор, НаборПолучателя) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	NULL КАК Поле1
		|ИЗ
		|	Справочник.УО_НаборыДанных КАК УО_НаборыДанных
		|ГДЕ
		|	УО_НаборыДанных.Ссылка = &НаборПолучателя
		|	И УО_НаборыДанных.Ссылка В ИЕРАРХИИ (&ТекущийНабор)";
	
	Запрос.УстановитьПараметр("НаборПолучателя", НаборПолучателя);
	Запрос.УстановитьПараметр("ТекущийНабор", ТекущийНабор);
	
	Результат = Запрос.Выполнить();

	Возврат НЕ Результат.Пустой();
	
КонецФункции

Функция ПредыдущийНабор(ТекущийНабор) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	УО_НаборыДанных.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.УО_НаборыДанных КАК УО_НаборыДанных
		|ГДЕ
		|	УО_НаборыДанных.Родитель = &Родитель
		|	И УО_НаборыДанных.Код < &Код
		|
		|УПОРЯДОЧИТЬ ПО
		|	УО_НаборыДанных.Код УБЫВ";
	
	Запрос.УстановитьПараметр("Код", ТекущийНабор.Код);
	Запрос.УстановитьПараметр("Родитель", ТекущийНабор.Родитель);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Возврат ВыборкаДетальныеЗаписи.Ссылка;
	КонецЦикла;
	
КонецФункции

Функция СледующийНабор(ТекущийНабор) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	УО_НаборыДанных.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.УО_НаборыДанных КАК УО_НаборыДанных
		|ГДЕ
		|	УО_НаборыДанных.Родитель = &Родитель
		|	И УО_НаборыДанных.Код > &Код
		|
		|УПОРЯДОЧИТЬ ПО
		|	УО_НаборыДанных.Код";
	
	Запрос.УстановитьПараметр("Код", ТекущийНабор.Код);
	Запрос.УстановитьПараметр("Родитель", ТекущийНабор.Родитель);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Возврат ВыборкаДетальныеЗаписи.Ссылка;
	КонецЦикла;
		
КонецФункции
