#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Элементы.ТипИсточника.Доступность = Объект.Родитель.ТипИсточника.Пустая();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПутьКИсточникуНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	// Получаем строковое представление значения Перечисления, что бы избежать обращения к серверу
	ТипИсточникаСтр = "" + Объект.ТипИсточника;
	
	Если ТипИсточникаСтр = "Файл" Тогда
		ВыбратьФайл();
	ИначеЕсли ТипИсточникаСтр = "Макет" Тогда
		ВыбратьМакет();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
АСИНХ Процедура ВыбратьФайл()

	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Диалог.Заголовок = "Выбрать файл схемы";
	Диалог.Фильтр = "Файл схемы компоновки данных|*.xml";
	Диалог.Каталог = Ждать КаталогДокументовАсинх();	// Может работать и веб-клиенте
	
	Ответ = Ждать Диалог.ВыбратьАсинх();
	
	Если Ответ <> Неопределено Тогда
		
		Объект.ПутьКИсточнику = Ответ.Получить(0);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьМакет()
	
	// Вычислять при каждом обращении или хранить в памяти -> плохо, хранить в РС -> хорошо
	ОткрытьФорму("РегистрСведений.УО_ДоступныеМакеты.Форма.ФормаВыбора",,
		ЭтотОбъект,,,, 
		Новый ОписаниеОповещения("ВыбратьМакетЗавершение", ЭтотОбъект), );
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьМакетЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		Объект.ПутьКИсточнику = Результат;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

