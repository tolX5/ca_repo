
Процедура Заполнить() Экспорт
	
	Если ТребуетсяЗаполнение() Тогда
		НаборЗаписей = СоздатьНаборЗаписей();
		ЗаполнитьНабор(НаборЗаписей);
		НаборЗаписей.Записать(Истина);
		Константы.УО_КонфигурацияИзменилась.Установить(Ложь);
	КонецЕсли;
	
КонецПроцедуры

Функция ТребуетсяЗаполнение()

	Возврат Константы.УО_КонфигурацияИзменилась.Получить();
	
КонецФункции

Процедура ЗаполнитьНабор(Набор)
	
	ВеткиОбъектов = Новый Массив;
	ВеткиОбъектов.Добавить(Метаданные.ОбщиеМакеты);
		
	ВеткиОбъектов.Добавить(Метаданные.ПланыОбмена);
	ВеткиОбъектов.Добавить(Метаданные.Справочники);
	ВеткиОбъектов.Добавить(Метаданные.Документы);
	ВеткиОбъектов.Добавить(Метаданные.Перечисления);
	ВеткиОбъектов.Добавить(Метаданные.Отчеты);
	ВеткиОбъектов.Добавить(Метаданные.Обработки);
	ВеткиОбъектов.Добавить(Метаданные.ПланыВидовХарактеристик);
	ВеткиОбъектов.Добавить(Метаданные.ПланыСчетов);
	ВеткиОбъектов.Добавить(Метаданные.ПланыВидовРасчета);
	ВеткиОбъектов.Добавить(Метаданные.РегистрыСведений);
	ВеткиОбъектов.Добавить(Метаданные.РегистрыНакопления);
	ВеткиОбъектов.Добавить(Метаданные.РегистрыБухгалтерии);
	ВеткиОбъектов.Добавить(Метаданные.РегистрыРасчета);
	ВеткиОбъектов.Добавить(Метаданные.БизнесПроцессы);
	ВеткиОбъектов.Добавить(Метаданные.Задачи);
	
	Для каждого КоллекцияОбъектов Из ВеткиОбъектов Цикл
		
		Для каждого ОбъектМетаданных Из КоллекцияОбъектов Цикл
			
			Если ЭтоМакетСКД(ОбъектМетаданных) Тогда
				
				ДобавитьЗаписьВНабор(Набор, ОбъектМетаданных);
				
			Иначе
				
				Свойство = Новый Структура("Макеты");
				ЗаполнитьЗначенияСвойств(Свойство, ОбъектМетаданных);
				
				Если Свойство.Макеты = Неопределено Тогда
					Продолжить;
				КонецЕсли;
				
				Для каждого ОбъектМетаданныхМакет Из ОбъектМетаданных.Макеты Цикл
					
					Если ЭтоМакетСКД(ОбъектМетаданныхМакет) Тогда
						ДобавитьЗаписьВНабор(Набор, ОбъектМетаданныхМакет);
					КонецЕсли;
					
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;

КонецПроцедуры

Функция ЭтоМакетСКД(ОбъектМетаданных)
	
	Свойство = Новый Структура("ТипМакета");
	ЗаполнитьЗначенияСвойств(Свойство, ОбъектМетаданных);
	
	Возврат Свойство.ТипМакета = Метаданные.СвойстваОбъектов.ТипМакета.СхемаКомпоновкиДанных;
	
КонецФункции

Процедура ДобавитьЗаписьВНабор(Набор, ОбъектМетаданных)

	Запись = Набор.Добавить();
	Запись.КлючЗаписи = Набор.Индекс(Запись) + 1;
	Запись.ПутьКМакету = ОбъектМетаданных.ПолноеИмя();

КонецПроцедуры
